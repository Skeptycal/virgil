// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// constants for openness
type Open { case UNKNOWN; case OPEN; case CLOSED; }

// Represents the value arguments, type arguments, and dynamic environment
// that is passed to an operator for it to evaluate itself.
class Arguments {
	def numArgs() -> int;
	def getArg(i: int) -> Val;
	def getType(i: int) -> Type;
	def getTypes() -> Array<Type>;
	def getProgram() -> Program;
	def fwCall(spec: IrSpec, recvr: Val, startArg: int, endArg: int) -> Result;
	def throw(ex: string, msg: string) -> Exception;
}
// An operator represents a strict computation from argument values to a result value.
class Operator(opcode: Opcode, typeArgs: Array<Type>, sig: Signature) {
	var openness: Open;			// lazily computed open / closed
	def subst(f: Type -> Type) -> Operator;
	def evaluate(args: Arguments) -> Result;
	def attr<T>() -> T { return OperatorOf<T>.!(this).attribute; }
	def checkOpenness() -> Open {
		// openness is not known. check all type args and operator type
		if (openness != Open.UNKNOWN) return openness;
		var hash = 0;
		// XXX: bail out earlier if a nested type is open
		for (t in typeArgs) hash = hash | t.hash;
		for (t in sig.paramTypes) hash = hash | t.hash;
		for (t in sig.returnTypes) hash = hash | t.hash;
		return openness = if((hash & TypeConst.OPEN_MASK) != 0, Open.OPEN, Open.CLOSED);
	}
	def isPolymorphic() -> bool {
		return checkOpenness() == Open.OPEN;
	}
	// get the type of this operator when the given parameter indices are bound
	def getResidualType(indexMap: Array<int>) -> Type {
		if (indexMap.length == 0) return sig.funcType();
		return Function.newType(getResidualParamType(indexMap), sig.returnType());
	}
	// get the list of residual types
	def getResidualParamTypeList(indexMap: Array<int>) -> List<Type> {
		if (indexMap.length == 0) return null;
		var rl: List<Type>;
		var p = sig.paramTypes, iv = indexMap.length - 1;
		for (i = p.length - 1; i >= 0; i--) {
			var pt = p[i];
			if (iv < 0 || i != indexMap[iv]) rl = List.new(pt, rl);
			else iv--;
		}
		return rl;
	}
	def getResidualParamType(indexMap: Array<int>) -> Type {
		return Tuple.TYPECON.create(getResidualParamTypeList(indexMap));
	}
	def equals(that: Operator) -> bool {
		if (this == that) return true;
		return this.opcode == that.opcode && Arrays.equal(this.typeArgs, that.typeArgs);
	}
}
// The actual implementation of operator, with an attribute of hidden type <T>
class OperatorOf<T> extends Operator {
	def attribute: T;
	def eval: (T, Arguments) -> Result;
	new(opcode: Opcode, typeArgs: Array<Type>, sig: Signature, attribute, eval)
		super(opcode, typeArgs, sig) { }
	def evaluate(args: Arguments) -> Result {
		return eval(attribute, args);
	}
	def subst(f: Type -> Type) -> Operator {
		if (openness == Open.CLOSED) return this;
		if (checkOpenness() == Open.OPEN) {
			return OperatorOf.new(opcode, Arrays.map(typeArgs, f), sig.subst(f), attribute, eval);
		}
		return this;
	}	
	def equals(op: Operator) -> bool {
		if (this.opcode != op.opcode) return false;
		if (this == op) return true;
		if (OperatorOf<T>.?(op)) {
			var that = OperatorOf<T>.!(op);
			return this.attribute == that.attribute &&
			       Arrays.equal(this.typeArgs, that.typeArgs);
		}
		return false;
	}
}
